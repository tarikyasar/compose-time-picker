plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "0.1"
}

def getArtifactId = { ->
    return "compose-time-picker"
}


android {
    namespace 'com.tarikyasar.compose_time_picker'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.tarikyasar.libraries' // Replace with group ID
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${githubProperties['gpr.usr']}/compose-time-picker")
            credentials {

                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

dependencies {
    // Compose
    implementation "androidx.activity:activity-compose:$composeActivity"
    implementation "androidx.compose.ui:ui:$compose"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose"

    // Material
    implementation "androidx.compose.material:material:$material"

    // Snapper
    implementation "dev.chrisbanes.snapper:snapper:$snapper"
}